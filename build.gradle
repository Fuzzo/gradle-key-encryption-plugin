plugins {
    id 'java-gradle-plugin'
    id 'eclipse'
    id 'maven-publish'
    id 'gradle-key-encryption-plugin' version '0.1'
}

group 'net.fuzzo'
version '0.1'

repositories {
    mavenCentral()
}

/*
dependencies {
	implementation 'dev.gradleplugins:gradle-api:7.6'
}
*/

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.9.1')
        }

        // Create a new test suite
        functionalTest(JvmTestSuite) {
            dependencies {
                // functionalTest test suite depends on the production code in tests
                implementation project()
            }

            targets {
                all {
                    // This test suite should run after the built-in test suite has run its tests
                    testTask.configure { shouldRunAfter(test) } 
                }
            }
        }
    }
}

gradlePlugin {
    // Define the plugin
    plugins {
        keyencryption {
            id = 'gradle-key-encryption-plugin'
            implementationClass = 'net.fuzzo.gradle.plugin.keyencryption.KeyEncryptionPlugin'
            displayName = 'gradle-key-encryption-plugin'
            description = 'Gradle plugin to encrypt and decrypt text using a secrey key stored in a Java keystore'
            tags.set(['encryption'])
        }
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
    // Include functionalTest as part of the check lifecycle
    dependsOn(testing.suites.functionalTest)
}
